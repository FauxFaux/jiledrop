apply plugin: 'java'
apply plugin: 'eu.appsatori.fatjar'
apply plugin: 'application'

sourceCompatibility = '1.8'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// The main class of the application
mainClassName = 'com.goeswhere.jiledrop.app.JileDropApplication'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}

// Set our project variables
project.ext {
    dropwizardVersion = '1.0.0-rc3'
}

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'io.dropwizard:dropwizard-core:' + dropwizardVersion,
            'io.dropwizard:dropwizard-assets:' + dropwizardVersion,
            'io.dropwizard:dropwizard-forms:' + dropwizardVersion,
            'io.dropwizard:dropwizard-migrations:' + dropwizardVersion,
            'io.dropwizard:dropwizard-jdbi:' + dropwizardVersion,
            'com.h2database:h2:1.4.192',
    )

    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.commons:commons-io:1.3.2'
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        force 'com.fasterxml.jackson.core:jackson-annotations:2.7.4'
        force 'org.slf4j:slf4j-api:1.7.21'
    }
}

build.dependsOn(fatJar)

task serve(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args 'server', 'src/dist/config/test.yml'
}

task migrate(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args 'db', 'migrate', 'src/dist/config/test.yml'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

fatJar {
    classifier 'fat'
    manifest {
        attributes 'Main-Class': mainClassName
    }
}
